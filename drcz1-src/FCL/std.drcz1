( {- quite usefull datastructures -}


{ the following one sets equality relation on SExps, induced by eq? (=) relation on atoms. }
(! eq-rec? (^ (e1 e2)
   	      (? ((@ e1) {=>} (= e1 e2))
                 ((@ e2) {=>} ())
                 (T {=>} (and (eq-rec? (. e1) (. e2))
                              (eq-rec? (, e1) (, e2)))))))
{<tmp> perhaps it should get classified as operator on lists? }

{= LISTs: ====================================================================}

{ We use these ones so often, we found no reason to prepend their names with
  any singature - after all McCarthy called his language LISP for a reason... }

(! empty? (^ (l) (= l ())))
(! first  (^ (l) (. l)))
(! second (^ (l) (. (, l))))
(! third  (^ (l) (. (, (, l)))))
(! fourth (^ (l) (. (, (, (, l))))))
(! rest   (^ (l) (, l)))
(! push   (^ (el l) (; el l)))

(! length (^ (l)
             (if l (+ 1 (length (rest l))) {else} 0)))

(! append (^ (a b)
             (if (empty? a)
                 b
                 {else} (push (first a) (append (rest a) b)))))

(! pair (^ (a b) (if a (push (; (first a) (first b)) (pair (rest a) (rest b))) {else} ())))

{ to niestety przy kompilacji nie zahabla, let to 'tylko lambda' i zapomina co znaczyło 'rev'
  wewnątrz 'rev'... 
(! reverse (^ (l)
              (let ((rev (^ (pend res)
                            (if (empty? pend)
                                res
                                (rev (rest pend) (push (first pend) res))))))
                   (rev l ()))))
}
(! reverse (^ (l) (_rev l ())))
(! _rev (^ (p r) (if p (_rev (, p) (; (. p) r)) r)))

(! nth (^ (n l)
          (if (= n 1)
              (first l)
              {else} (nth (- n 1) (rest l)))))

(! position (^ (el l) (_pos el l 1)))
(! _pos (^ (el l n)
   	   (? ((= l ()) {=>} ())
              ((= el (. l)) {=>} n)
              (T {=>} (_pos el (, l) (+ n 1))))))

(! map (^ (proc l)
          (if (empty? l)
              ()
              {else} (push (proc (first l)) (map proc (rest l))))))

{ since drcz1 does not allow to use free variables, we sometimes need an awkward version of map that
  passes additional arguments to proc: }

{mapg: [Exp x SExp -> SExp] x SExp x List<SExp> -> List<SExp> }
(! mapg (^ (proc args l) (if l (push (proc (first l) args) (mapg proc args (rest l))) {else} ())))

(! member? (^ (el l)
              (? ((empty? l) {=>} ())
                 ((= el (first l)) {=>} T)
                 (T {=>} (member? el (rest l))))))

(! uniq (^ (l)
           (? ((empty? l) {=>} ())
              ((member? (first l) (rest l)) {=>} (uniq (rest l)))
              (T {=>} (push (first l) (uniq (rest l)))))))

{ a te to w ogóle powinny się nazywać SET:... chyba... pomyślimy potem. }

(! intersect
   (^ (l1 l2)
      (if l1
          (if (member? (first l1) l2)
              (push (first l1) (intersect (rest l1) l2))
              {else} (intersect (rest l1) l2))
          {else} ())))

(! diff
   (^ (l1 l2)
      (if l1
          (if (member? (first l1) l2)
              (diff (rest l1) l2)
	      {else} (push (first l1) (diff (rest l1) l2)))
          {else} ())))

(! subset? (^ (l1 l2)
              (? ((empty? l1) {=>} T)
                 ((member? (first l1) l2) {=>} (subset? (rest l1) l2))
                 (T {=>} ()))))
      

{ sometimes it happens that we want to use member? with more complex "identity [equality] relations" }
(! memberg? (^ (el l eq?)
               (? ((empty? l) {=>} ())
                  ((eq? el (first l)) {=>} T)
                  (T {=>} (memberg? el (rest l) eq?)))))

(! reduce (^ (op neutr l)
             (if l
                 (op (. l) (reduce op neutr (, l)))
                 {else} neutr)))


(! last (^ (l)
           (if (rest l) {!}
               (last (rest l))
               {else} (first l))))

(! drop-last-element (^ (l)
                     (if (rest l) {!}
                         (push (first l)
                               (drop-last-element (rest l)))
                         {else} ())))

(! drop (^ (el l)
           (? ((= l ()) {=>} ())
              ((= (. l) el) {=>} (, l))
              (T {=>} (; (. l) (drop el (, l)))))))

(! drop-all (^ (el l)
               (? ((= l ()) {=>} ())
                  ((= (. l) el) {=>} (drop-all el (, l)))
                  (T {=>} (; (. l) (drop-all el (, l)))))))

{= A-LISTs: ==================================================================}

{ uwaga: skumaj, że przecież możnaby z AL zrobić obiekt pełną pizdą - wystarczy żeby new prócz zainicjalizowania dostawał swoją procedurę eq? do kluczy
  - wtedy każda operacja miast brać argument eq? mogłaby sobie wyłuskiwać z AL odpowiedni porównywacz i przekazywać do ogólnego lookup/add/update... }

(! AL:new (^ (keys vals) (pair keys vals)))

(! AL:keys (^ (al)
              (if (empty? al)
                  ()
                  {else} (push (. (first al))
                               (AL:keys (rest al))))))

(! AL:add (^ (key val al)
             (push (; key val) al)))

(! AL:has-key? (^ (key al eq?)
                  (if al
                      (if (eq? (. (first al)) key)
                          T
                          (AL:has-key? key (rest al) eq?))
                      {else} ())))

(! AL:update-all (^ (al new-val)
                    (if al
                        (let ((key {<-} (. (first al)))
                              (val {<-} (, (first al)))
                              (ral {<-} (rest al))
                              (new-val {<~} new-val))
                             {in} (push (; key (new-val key val))
                                        (AL:update-all ral new-val)))
                        {else} ())))

{ notice these two take additional argument 'eq?' - a procedure of type ValxVal->Boolean
  settling equivalence of two values. }

(! AL:update (^ (key val al eq?)
                (? ((empty? al) {=>} (AL:add key val al))
                   ((eq? (. (first al)) key) {=>} (AL:add key val (rest al)))
                   (T {=>} (push (first al) (AL:update key val (rest al) eq?))))))

(! AL:drop (^ (key al eq?)
              (? ((empty? al) {=>} ())
                 ((eq? (. (first al)) key) {=>} (rest al))
                 (T {=>} (push (first al) (AL:drop key (rest al) eq?))))))
                    
(! AL:assoc (^ (key al eq?)
               (if (empty? al)
                   (){'ERR}
                   {else} (if (eq? key (. (first al)))
                              (, (first al))
                              {else} (AL:assoc key (rest al) eq?)))))

(! AL:drop-many (^ (keys al eq?)
                   (? ((empty? al) {=>} ())
                      ((memberg? (. (first al)) keys eq?) {=>} (AL:drop-many keys (rest al) eq?))
                      (T {=>} (push (first al) (AL:drop-many keys (rest al) eq?))))))

(! AL:filter-by-keys (^ (keys al eq?)
                        (? ((empty? al)
                            {=>} ())
                           ((memberg? (. (first al)) keys eq?)
                            {=>} (push (first al) (AL:filter-by-keys keys (rest al) eq?)))
                           (T
                            {=>} (AL:filter-by-keys keys (rest al) eq?)))))

{=============================================================================} )